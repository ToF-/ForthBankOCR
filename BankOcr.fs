\ BankOcr

3  CONSTANT OCR-WIDTH
3  CONSTANT OCR-HEIGHT

VARIABLE ACCOUNT-SIZE 
9  ACCOUNT-SIZE !

: OCR-LINE-SIZE ACCOUNT-SIZE @ OCR-WIDTH * ;

CREATE OCR-BUFFER 
OCR-LINE-SIZE OCR-HEIGHT * ALLOT

: BAR? 32 <> 1 AND ;

: OCR-OFFSET ( i -- pos of char i in ocr digit )
    ?DUP 0= IF 1 ELSE
    1- OCR-HEIGHT /MOD 
    1+ OCR-LINE-SIZE * 
    SWAP + 
    THEN ;
       
: OCR-ADDRESS ( n i -- addr  char i or nth ocr digit ) 
    OCR-OFFSET SWAP OCR-WIDTH * 
    + OCR-BUFFER + ;

: EMPTY-OCR-BUFFER ( -- )
    OCR-BUFFER OCR-LINE-SIZE OCR-HEIGHT * 20 FILL ;

: OCR>PATTERN ( n -- b )
    DUP 0 OCR-ADDRESS C@ BAR? ( n b )
    7 1 DO 1 LSHIFT OVER I OCR-ADDRESS C@ BAR?  OR LOOP
    NIP ;

: CTABLE CREATE DOES> + C@ ;
2 BASE !
CTABLE DIGITS  
1101111 C, 0001001 C, 1011110 C, 1011011 C, 0111001 C,  
1110011 C, 1110111 C, 1001001 C, 1111111 C, 1111011 C,
DECIMAL 
2 BASE !
: DIGIT>CHAR ( d -- c ) 
    110000 OR ;
DECIMAL

CHAR '?' CONSTANT NOT-FOUND
: PATTERN>DIGIT ( b -- c )
    NOT-FOUND 10 0 DO 
        OVER I DIGITS = IF DROP I DIGIT>CHAR LEAVE THEN 
    LOOP NIP ;
