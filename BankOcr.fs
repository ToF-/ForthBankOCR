\ BankOcr

: CTABLE CREATE DOES> + C@ ;

3      CONSTANT WIDTH
3      CONSTANT HEIGHT
CHAR ? CONSTANT NOT-FOUND
VARIABLE ACCOUNT-SIZE 9 ACCOUNT-SIZE !
: SIZE ACCOUNT-SIZE @ ;
CREATE ACCOUNT SIZE ALLOT
: .ACCOUNT ACCOUNT SIZE TYPE ;
: OCR-WIDTH SIZE WIDTH * ;
: OCR-LENGTH OCR-WIDTH HEIGHT * ;
: OCR-POS WIDTH * ;

2 BASE !
CTABLE DIGITS  
010101111 C, 000001001 C, 010011110 C, 010011011 C, 000111001 C,  
010110011 C, 010110111 C, 010001001 C, 010111111 C, 010111011 C,
DECIMAL 

: BAR?        32 <> 1 AND ;
: OCR-BIT-POS HEIGHT /MOD OCR-WIDTH * + ;
: OCR-BIT     OCR-BIT-POS + C@ BAR? ;
: OCR>PATTERN 0 9 0 DO  1 LSHIFT OVER I OCR-BIT OR  LOOP NIP ;
2 BASE !
: >CHAR 110000 OR ;
: >INT  001111 AND ;
DECIMAL

: PATTERN>DIGIT NOT-FOUND 10 0 DO OVER I DIGITS = IF DROP I LEAVE THEN LOOP NIP >CHAR ;
: OCR>DIGIT     OCR-POS + OCR>PATTERN PATTERN>DIGIT ;
: OCR>ACCOUNT   SIZE 0 DO DUP I OCR>DIGIT ACCOUNT I + C!  LOOP DROP ;
: ILLEGIBLE?    FALSE SIZE 0 DO I ACCOUNT + C@ NOT-FOUND = OR LOOP ;
: ERROR?        0 SIZE 0 DO  ACCOUNT I + C@  >INT SIZE I - * + LOOP 11 MOD ;
: PROCESS-OCR   OCR>ACCOUNT .ACCOUNT SPACE ILLEGIBLE? IF ."  ILL" ELSE ERROR? IF ."  ERR" THEN THEN CR ;
VARIABLE LINE-OFFSET 
: INIT-PAD      0 LINE-OFFSET ! ;
: NEXT-PAD      PAD LINE-OFFSET @ + ;
: PROCESS-LINE  LINE-OFFSET +! LINE-OFFSET @ OCR-LENGTH >= IF PAD PROCESS-OCR INIT-PAD THEN ;
: LINE-WIDTH    OCR-WIDTH 2 + ;
: PROCESS-FILE  ( c-addr,u -- )
    R/O OPEN-FILE THROW >R INIT-PAD
    BEGIN NEXT-PAD LINE-WIDTH R@ READ-LINE THROW WHILE PROCESS-LINE REPEAT R> 2DROP ;

: MAIN 
    NEXT-ARG 2DUP 0 0 D<> IF
        PROCESS-FILE
    ELSE 2DROP THEN ;

    
