\ run tests with: gforth Tests.fs

S" BankOcr.fs" REQUIRED

CHAR _ CONSTANT BAR
CHAR | CONSTANT PIPE

: LINE ROT C, SWAP C, C, ;
:  _   BL BAR BL     LINE ;

: |.|  PIPE BL PIPE  LINE ;

: |_   PIPE BAR BL   LINE ;

:  _|  BL  BAR  PIPE LINE ;

: |    BL  BL  PIPE  LINE ;

: |_|  PIPE BAR PIPE LINE ;

: ...  BL  BL  BL    LINE ;


: CHECK-OCR-ADDRESS ( n i p -- f|t )
    OCR-BUFFER + -ROT OCR-ADDRESS = ;

CREATE OCR-TEST
 _  ...  _   _  ...  _   _  _   _   _
|.|   |  _|  _| |_| |_  |_   | |_| |_|
|_|   | |_   _|   |  _| |_|  | |_|  _|

OCR-TEST      30 TYPE CR
OCR-TEST 30 + 30 TYPE CR
OCR-TEST 60 + 30 TYPE CR

10 ACCOUNT-SIZE !

: AUTOTEST
    ASSERT( BL   BAR? 0 = )
    ASSERT( PIPE BAR? 1 = )
    ASSERT( BAR  BAR? 1 = )

    ASSERT( 0 0  1 CHECK-OCR-ADDRESS )
    ASSERT( 0 1 30 CHECK-OCR-ADDRESS )
    ASSERT( 0 2 31 CHECK-OCR-ADDRESS )
    ASSERT( 0 3 32 CHECK-OCR-ADDRESS )
    ASSERT( 0 4 60 CHECK-OCR-ADDRESS )
    ASSERT( 0 5 61 CHECK-OCR-ADDRESS )
    ASSERT( 0 6 62 CHECK-OCR-ADDRESS )
    ASSERT( 1 0  4 CHECK-OCR-ADDRESS )
    ASSERT( 1 1 33 CHECK-OCR-ADDRESS )
    ASSERT( 1 2 34 CHECK-OCR-ADDRESS )
    ASSERT( 9 6 89 CHECK-OCR-ADDRESS )


    OCR-TEST OCR-BUFFER 90 CMOVE
    ASSERT( 0 [ 2 BASE ! ] OCR>PATTERN 1101111 = [ DECIMAL ] )
    ASSERT( 1 [ 2 BASE ! ] OCR>PATTERN 0001001 = [ DECIMAL ] )
    ASSERT( 5 [ 2 BASE ! ] OCR>PATTERN 1110011 = [ DECIMAL ] )

    ." TESTS PASSED"
    DEPTH ?DUP IF CR .S THEN 
;

AUTOTEST CR

BYE
