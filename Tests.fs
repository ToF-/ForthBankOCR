\ run tests with: gforth Tests.fs

S" BankOcr.fs" REQUIRED

CHAR _ CONSTANT BAR
CHAR | CONSTANT PIPE

: LINE ROT C, SWAP C, C, ;
: _    BL BAR BL     LINE ;
: |.|  PIPE BL PIPE  LINE ;
: |_   PIPE BAR BL   LINE ;
: _|   BL  BAR  PIPE LINE ;
: |    BL  BL  PIPE  LINE ;
: |_|  PIPE BAR PIPE LINE ;
: ...  BL  BL  BL    LINE ;


: CHECK-OCR-ADDRESS ( n i p -- f|t )
    OCR-BUFFER + -ROT OCR-ADDRESS = ;

CREATE OCR-TEST
 _  ...  _   _  ...  _   _  _   _   _
|.|   |  _|  _| |_| |_  |_   | |_| |_|
|_|   | |_   _|   |  _| |_|  | |_|  _|

OCR-TEST      30 TYPE CR
OCR-TEST 30 + 30 TYPE CR
OCR-TEST 60 + 30 TYPE CR



: AUTOTEST
    ASSERT( BL   BAR? 0 = )
    ASSERT( PIPE BAR? 1 = )
    ASSERT( BAR  BAR? 1 = )

    ASSERT( 0 0  1 CHECK-OCR-ADDRESS )
    ASSERT( 0 1 27 CHECK-OCR-ADDRESS )
    ASSERT( 0 2 28 CHECK-OCR-ADDRESS )
    ASSERT( 0 3 29 CHECK-OCR-ADDRESS )
    ASSERT( 0 4 54 CHECK-OCR-ADDRESS )
    ASSERT( 0 5 55 CHECK-OCR-ADDRESS )
    ASSERT( 0 6 56 CHECK-OCR-ADDRESS )
    ASSERT( 1 0  4 CHECK-OCR-ADDRESS )
    ASSERT( 1 1 30 CHECK-OCR-ADDRESS )
    ASSERT( 1 2 31 CHECK-OCR-ADDRESS )
    ASSERT( 8 6 80 CHECK-OCR-ADDRESS )
    


    EMPTY-OCR-BUFFER 
    BAR  0 0 OCR-ADDRESS C!
    PIPE 0 1 OCR-ADDRESS C!
    BL   0 2 OCR-ADDRESS C!
    PIPE 0 3 OCR-ADDRESS C!
    PIPE 0 4 OCR-ADDRESS C!
    BAR  0 5 OCR-ADDRESS C!
    PIPE 0 6 OCR-ADDRESS C!

    [ 2 BASE ! ]
    ASSERT( 0 OCR>PATTERN 1101111 = )
    [ DECIMAL ]

    


    ." TESTS PASSED"
    DEPTH ?DUP IF CR .S THEN 
;

AUTOTEST CR

BYE
