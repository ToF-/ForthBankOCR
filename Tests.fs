\ run tests with: gforth Tests.fs

S" BankOcr.fs" REQUIRED

: TEST-SAMPLE-012345678
\  0         1         2         3         4         5         6         7         8
\  01234567890123456789012345678901234567890123456789012345678901234567890123456789012
S"  _     _  _     _  _  _  _ | |  | _| _||_||_ |_   ||_||_|  ||_  _|  | _||_|  ||_|"
OCR-BUFFER SWAP CMOVE ;

: CHECK-SAMPLE-012345678 ( -- t|f )
    TRUE
    9 0 DO ACCOUNT I + C@ 15 AND I <> IF DROP FALSE THEN LOOP ;

: CHECKS
    TEST-SAMPLE-012345678
    ASSERT( 32  BAR? 0 = ) 
    ASSERT( 95  BAR? 1 = ) 
    ASSERT( 124 BAR? 1 = )
    [ 2 BASE ! ]
    ASSERT( 100101 1 BIT! 1001011 = )
    ASSERT( 111001 0 BIT! 1110010 = )
    ASSERT( 110011 BIT@ 1 = SWAP 11001 = AND )
    [ DECIMAL ]
    ASSERT( 0 0 OCR-OFFSET 0  = )
    ASSERT( 0 2 OCR-OFFSET 2  = )
    ASSERT( 0 3 OCR-OFFSET 27 = )
    ASSERT( 0 8 OCR-OFFSET 56 = )
    ASSERT( 8 6 OCR-OFFSET 78 = )
    ASSERT( 8 7 OCR-OFFSET 79 = )
    ASSERT( 8 8 OCR-OFFSET 80 = )
    ASSERT( 0 OCR>PATTERN [ 2 BASE ! ] 010101111 = [ DECIMAL ] )
    ASSERT( 1 OCR>PATTERN [ 2 BASE ! ] 000001001 = [ DECIMAL ] )
    ASSERT( 8 OCR>PATTERN [ 2 BASE ! ] 010111111 = [ DECIMAL ] )
    ASSERT( [ 2 BASE ! ] 011111111 [ DECIMAL ] PATTERN>DIGIT NOT-FOUND =  )
    ASSERT( [ 2 BASE ! ] 000111001 [ DECIMAL ] PATTERN>DIGIT [CHAR] 4 =  )
    ASSERT( 0 OCR>PATTERN PATTERN>DIGIT [CHAR] 0 = ) 
    ASSERT( 1 OCR>PATTERN PATTERN>DIGIT [CHAR] 1 = ) 
    ASSERT( 2 OCR>PATTERN PATTERN>DIGIT [CHAR] 2 = ) 
    ASSERT( 3 OCR>PATTERN PATTERN>DIGIT [CHAR] 3 = ) 
    ASSERT( 4 OCR>PATTERN PATTERN>DIGIT [CHAR] 4 = ) 
    ASSERT( 5 OCR>PATTERN PATTERN>DIGIT [CHAR] 5 = ) 
    ASSERT( 6 OCR>PATTERN PATTERN>DIGIT [CHAR] 6 = ) 
    ASSERT( 7 OCR>PATTERN PATTERN>DIGIT [CHAR] 7 = ) 
    ASSERT( 8 OCR>PATTERN PATTERN>DIGIT [CHAR] 8 = ) 
    \ altering an ocr digit
    OCR-BUFFER 1+ 32 SWAP C!
    ASSERT( 0 OCR>PATTERN PATTERN>DIGIT NOT-FOUND = ) 
    \ fixing the ocr digit back
    OCR-BUFFER 1+ 95 SWAP C!
    ASSERT( ACCOUNT ACCOUNT-SIZE ERASE 
            OCR>ACCOUNT
            CHECK-SAMPLE-012345678 )
    PRINT-ACCOUNT CR
    \ sumchecks account 457508000
    S" 457508000" ACCOUNT SWAP CMOVE 
    PRINT-ACCOUNT CR        
    ASSERT( SUMCHECK-ERROR? 0= )
    TEST-SAMPLE-012345678
    \ altering an ocr digit
    OCR-BUFFER 1+ 32 SWAP C!
    OCR>ACCOUNT
    PRINT-ACCOUNT CR
    ASSERT( ILLEGIBLE? )
    \ fixing the ocr digit back
    OCR-BUFFER 1+ 95 SWAP C!
    PRINT-OCR
    \ puts a 7 OCR digit in first pos
    7 DIGIT>PATTERN 0 PATTERN>OCR
    PRINT-OCR
    \ converts an account to OCR
    S" 480765312" ACCOUNT SWAP CMOVE
    ACCOUNT>OCR
    PRINT-OCR
            
; 
CR CHECKS .S CR BYE
